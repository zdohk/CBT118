*          DATA SET CBT1277    AT LEVEL 006 AS OF 08/28/83
PUTXREF  TITLE 'PROGRAM TO CROSS REFERENCE A PUT TAPE BY FMID'          00001
*********************************************************************** 00002
*                                                                     * 00003
*              PUTXREF - VERSION 3.0                                  * 00004
*                                                                     * 00005
*        JERRY LAWSON  860-547-5000 EXT 2960                          * 00006
*        HARTFORD INSURANCE GROUP,                                    * 00007
*        690 ASYLUM AVE.                                              * 00008
*        HARTFORD, CT.  06115                                         * 00009
*                                                                     * 00010
*        FUNCTION:                                                    * 00011
*        THIS PROGRAM TAKES THE PUT PTF FILE (FILE 1 FROM A           * 00012
*        STANDARD IBM PUT TAPE), AND PRODUCES A CROSS                 * 00013
*        REFERENCE OF THE PTF'S INCLUDED, SORTED BY FMID.             * 00014
*        IT CAN TAKE CONCATENATED INPUT, BUT THE TAPE IDENITY         * 00015
*        ON WHICH THE PTF IS FOUND WILL BE LOST. THE PROGRAM          * 00016
*        SHOULD BE USEFUL TO QUICKLY IDENTIFY WHAT PTF'S              * 00017
*        ARE ON A GIVEN TAPE, WHAT FMID'S HAVE MAINTENANCE ON         * 00018
*        A TAPE, AND TO DOUBLE CHECK THE STERLING FORREST             * 00019
*        CROSS REFERENCE (IT LIES ONCE IN A WHILE.)                   * 00020
*                                                                     * 00021
*        DEPENDANCIES:                                                * 00022
*        1.    THIS PROGRAM HAS AN INVOKED INCORE SORT.               * 00023
*              IT LINKS TO THE ENTRY POINT OF "SORT".                 * 00024
*              CHANGE THE LINK MACRO IF YOU DO NOT SUPPORT            * 00025
*              THAT NAME OR ALIAS. YOU WILL NEED ABOUT                * 00026
*              300K FOR THE WHOLE PROGRAM AND SORT.                   * 00027
*        2.    TITLES.  THIS FILE (FOR FMID TITLES) IS                * 00028
*              OPTIONAL. IF NOT INCLUDED, YOU WILL GET A              * 00029
*              WARNING MESSAGE ON YOUR SYSOUT, AND NO TITLES.         * 00030
*              I HAVE INCLUDED THE FILE IN THE JCL, I                 * 00031
*              CREATED THE FILE BY DOING THE FOLLOWING:               * 00032
*              A. ALLOCATE A DATA SET ON TSO  - NORMAL STUFF -        * 00033
*                  RECFM=FB,LRECL=80                                  * 00034
*              B.  ASSIGN THAT DATA SET TO DD NAME OF OZPRINT         * 00035
*              C.  EXECUTE YOUR INFO - SYS CLIST OR COMMAND           * 00036
*              D.  ISSUE A 'DEF A' COMMAND TO INFO (SEARCH            * 00037
*                  ALL FILES)                                         * 00038
*              E.  DO A KEY WORD SEARCH AS FOLLOWS -                  * 00039
*                  KWS FMID XREF      I GOT ABOUT 22 HITS,            * 00040
*                  BUT THE FIRST ONE HAD A TITLE OF                   * 00041
*                  'THIS IS A CROSS REFERENCE OF MVS FMIDS',          * 00042
*                  OR SOMETHING SIMILAR. BE SURE YOU                  * 00043
*                  HAVE THE LATEST ONE                                * 00044
*              F.  SELECT THE CORRECT ONE, THE ISSUE A                * 00045
*                  'PRINT UP' COMMAND. THE OUTPUT WILL GO             * 00046
*                  TO THE DATA SET ALLOCATED TO THE OZPRINT           * 00047
*                  DDNAME                                             * 00048
*              G.  END YOUR INFO SESSION                              * 00049
*              H.  EDIT YOUR PRINT FROM THE INFO SESSION              * 00050
*                  YOU WANT TO DELETE ALL LINES THAT DO               * 00051
*                  NOT HAVE AN FMID IN COLUMN 3. NOTE THAT DELETED      00052
*                  FMIDS HAVE A * IN COLUMN 3, FOR BETTER             * 00053
*                  DOCUMENTATION I DELETE THE STAR ANDMOVE THE        * 00054
*                  FMID BACK TO 3, BUT THIS IS                        * 00055
*                  OPTIONAL.                                          * 00056
*              I.  AFTER YOU HAVE SAVED THE DATA SET,                 * 00057
*                  SORT IT ON COLUMN 3 FOR A LENGTH OF 7.             * 00058
*              J.  THE TITLE LIST YOU HAVE JUST CREATED               * 00059
*                  CAN BE EITHER INCLUDED IN THE JCL STREAM           * 00060
*                  OR KEPT IN A SEPERATE DATA SET, AND POINTED        * 00061
*                  TO BY THE JCL.                                     * 00062
*        3.    REGS MACRO. THE STANDARD REGISTER EQUATES              * 00063
*              SUCH AS IN R1      EQU    1      ETC.                  * 00064
*              NOTE - REMOVED ON 3/10/82                              * 00065
*                                                                     * 00066
*        CHANGES FOR RELEASE 3.0                                      * 00067
*        1.    CHANGED INPUT LOCATION OF TITLES FROM COLUMN 2 TO      * 00068
*              COLUMN 3. THIS ALLOWS USE OF THE OUTPUT OF THE         * 00069
*              OZPRINT DD STATEMENT WITHOUT HAVING TO DELETE          * 00070
*              THE CARRIAGE CONTROL INFO COMPLETELY. SEE THE INFO IN  * 00071
*              2. ABOVE FOR DETAILS ON HOW TO CREATE THE TITLE        * 00072
*              FILE.                                                  * 00073
*        2.    EXTENDED SIZE OF THE TITLE FIELD TO INCLUDE            * 00074
*              THE COMPONENT ID NOW THAT IBM HAS                      * 00075
*              INCLUDED IT IN THE FMID XREF.                          * 00076
*                                                                     * 00077
*        CHANGES FOR RELEASE 2.0:                                     * 00078
*        1. ADDED BLOCKSIZES TO DCB MACROS TO CORRECT DEPENDENCY      * 00079
*           ON LOCAL MODS                                             * 00080
*        2. CORRECTED SORT FIELDS STATEMENT IN PROGRAM. ONLY USED IF  * 00081
*           IBM SORT IS INVOKED. NOTE THAT PARM ERROR SEEMS TO BE     * 00082
*           A STANDARD FEATURE WITH THIS PROGRAM - I CAN FIND NO ERROR* 00083
*        3. SYSOUT JCL DD STATEMENT ADDED FOR IBM SORTS               * 00084
*                                                                     * 00085
*        MACROS.                                                      * 00086
*        SAVE      OPEN      CLOSE      GET                           * 00087
*        PUT       LINK      WTO        ABEND (U128 - BAD SORT)       * 00088
*        DCB                                                            00089
*                                                                     * 00090
*        REGISTER ASSIGNMENTS - NORMAL LINKAGE CONVENTIONS            * 00091
*              REG 12              BASE REGISTER                      * 00092
*              REG 2               BAL REGISTER                       * 00093
*              REG 3               BCT REGISTER - PHASE 1             * 00094
*              REG 4               POINTER TO PTF INPUT RECORD        * 00095
*              REG 5               OUTPUT POSITION POINTER - PHASE 2  * 00096
*              REG 6               BCT REGISTER - PHASE 2             * 00097
*              REG 7               RETURN ADDRESS FOR EODAD (TITLES)  * 00098
*********************************************************************** 00099
         EJECT                                                          00100
PUTXREF  CSECT                                                          00101
         SAVE  (14,12),,*                                               00102
         LR    R12,R15                                                  00103
         USING PUTXREF,R12         REG 12 IS BASE                       00104
         LA    R15,SAVE                                                 00105
         ST    R15,8(R13)                                               00106
         ST    R13,SAVE+4                                               00107
         LR    R13,R15                                                  00108
         OPEN  (SYSIN,INPUT)                                            00109
         OPEN  (SORT,OUTPUT)                                            00110
         SPACE 2                                                        00111
*---------------------------------------------------------------------* 00112
*        THIS SECTION SEARCHES THE INPUT FOR ++PTF AND ++VER          * 00113
*        STATEMENTS ON THE PUT TAPE. ALL OTHERS ARE REJECTED,         * 00114
*        BUT A BAD PTF MIGHT CAUSE EXCESS SCANNING TO BE DONE.        * 00115
*---------------------------------------------------------------------* 00116
PX010    EQU   *                                                        00117
         BAL   R2,PXGET            LINK TO COMMON READ ROUTINE          00118
         CLC   0(2,R4),=C'++'      IS THIS AN SMP CONTROL CARD?         00119
         BNE   PX010               NO - BACK FOR ANOTHER                00120
         LA    R4,2(R4)            YES - POINT PAST THE ++              00121
         SH    R3,=H'2'            DECREMENT LENGTH FOR LOOP            00122
PX015    EQU   *                                                        00123
         CLI   0(R4),C' '          ANYTHING HERE YET?                   00124
         BNE   PX020               YES - GO CHECK IT OUT                00125
         LA    R4,1(R4)            NO - THEN POINT TO NEXT POSITION     00126
         BCT   R3,PX015            LOOP DE LOOP                         00127
         BAL   R2,PXGET            WOOPS - OUT OF RECORD                00128
         B     PX015               BACK AGAIN WITH ANOTHER RECORD       00129
PX020    EQU   *                                                        00130
         CLC   0(3,R4),=C'PTF'     IS THIS A ++PTF CARD?                00131
         BE    PX100               YES - GO GETPTF NUMBER               00132
         CLC   0(3,R4),=C'VER'     NO - THEN IS IT A ++VER?             00133
         BE    PX200               YES - THEN GO FIND THE FMID          00134
         B     PX010               NO - THEN THIS IS NG - START OVER.   00135
         EJECT                                                          00136
*---------------------------------------------------------------------* 00137
*        THIS SECTION GETS THE PTF NUMBER FROM THE SMP CONTROL        * 00138
*        CARDS. IT ASSUMES THIS TO BE THE FIRST NON-BLANK             * 00139
*        CHARACTER AFTER THE FIRST LEFT PAREN AFTER                   * 00140
*        THE KEYWORD 'PTF'                                            * 00141
*---------------------------------------------------------------------* 00142
PX100    EQU   *                                                        00143
         LA    R4,3(R4)            BUMP PAST THE PTF KEYWORD            00144
         SH    R3,=H'3'            DECREMENT LOOP CONTROL               00145
         C     R3,=F'0'            OUT OF RECORD?                       00146
         BH    PX110               NO - ON WITH THE SHOW                00147
         BAL   R2,PXGET            YES - LINK TO COMMON GET ROUTINE     00148
PX110    EQU   *                                                        00149
         CLI   0(R4),C'('          THIS A LEFT PAREN?                   00150
         BE    PX150               YES - THEN GO SAVE PTF NUMBER        00151
         LA    R4,1(R4)            NO - POINT TO NEXT POSITION          00152
         BCT   R3,PX110            LOOP AGAIN                           00153
         BAL   R2,PXGET            OUT OF RECORD AGAIN                  00154
         B     PX110                                                    00155
PX150    EQU   *                                                        00156
         LA    R4,1(R4)            POINT PAST LEFT PAREN                00157
         BCTR  R3,0                DECREMENT FOR THE PAREN              00158
         CLI   0(R4),C' '          ANYTHING HERE?                       00159
         BNE   PX160               YES - THEN GO SAVE IT                00160
         LA    R4,1(R4)            NO - POINT TO NEXT                   00161
         BCT   R3,PX150             AND LOOP (GETTING DIZZY YET?)       00162
         BAL   R2,PXGET            OFF TO THE COMMONS AGAIN             00163
         B     PX150                                                    00164
PX160    EQU   *                                                        00165
         MVC   OUTPTF(7),0(R4)     SAVE PTF NUMBER                      00166
         B     PX010               BACK TO FIND THE FMID                00167
         EJECT                                                          00168
*---------------------------------------------------------------------* 00169
*        THIS SECTION GETS THE FMID FROM THE ++VER STATEMENT. IT      * 00170
*        ASSUMES THE FMID TO BE THE FIRST NON-BLANK CHARACTER         * 00171
*        AFTER THE FIRST LEFT PAREN AFTER THE 'FMID' KEYWORD.         * 00172
*        WHEN THIS IS LOCATED, A RECORTED IS WRITTEN TO THE SORT FILE.* 00173
*---------------------------------------------------------------------* 00174
PX200    EQU   *                                                        00175
         LA    R4,3(R4)            POINT PAST THE VER KEYWORD           00176
         SH    R3,=H'3'            AND DECREMENT THE LOOP CONTROL       00177
         C     R3,=F'0'            OUT OF RECORD?                       00178
         BH    PX210               NO - ON WITH THE SHOW                00179
         BAL   R2,PXGET            YES - OFF AND GET ANOTHER            00180
PX210    EQU   *                                                        00181
         CLC   0(4,R4),=C'FMID'    IS THIS THE FMID?                    00182
         BE    PX220               YES - THEN GO FIND THE ACTUAL FMID   00183
         LA    R4,1(R4)            NO - POINT TO NEXT LITTLE DEVIL      00184
         BCT   R3,PX210            WE'LL GET HIM YET                    00185
         BAL   R2,PXGET            WOOPS OUT OF DATA                    00186
         B     PX210               BACK WITH A NEW RECORD               00187
PX220    EQU   *                                                        00188
         LA    R4,4(R4)            POINT PAST THE KEYWORD               00189
         SH    R3,=H'4'            DECREMENT THE LOOP CONTROL           00190
         C     R3,=F'0'            OUT OF DATA?                         00191
         BH    PX230               NO - THEN FIND THE LITTLE DEVIL      00192
         BAL   R2,PXGET            OUT OF DATA AGAIN                    00193
PX230    EQU   *                                                        00194
         CLI   0(R4),C'('          LEFT PAREN YET?                      00195
         BE    PX240               YES - ALMOST THERE                   00196
         LA    R4,1(R4)            POINT TO NEXT PLACE                  00197
         BCT   R3,PX230            LOOP DE LOOP                         00198
         BAL   R2,PXGET            WOOPS - THE SAFTEY VALVE AGAIN       00199
         B     PX230               KEEP ON PLUGGIN.                     00200
PX240    EQU   *                                                        00201
         LA    R4,1(R4)            POINT PAST THE PAREN                 00202
         BCTR  R3,0                DON'T FORGET THE LOOP CONTROL        00203
         CLI   0(R4),C' '          THEY DIDN'T SLIP IN ANOTHER BLANK?   00204
         BNE   PX250               NOPE - THEN WE GOT IT ALL!!!!        00205
         LA    R4,1(R4)            YES - THOSE LITTLE BUGGERS           00206
         BCT   R3,PX240            LOOP ONE MORE TIME                   00207
         BAL   R2,PXGET            COMMONER AND COMMONER                00208
         B     PX240                                                    00209
PX250    EQU   *                                                        00210
         MVC   OUTFMID,0(R4)       SAVE THE FMID NUMBER                 00211
         PUT   SORT,OUTPTF         WRITE THE RECORD                     00212
         B     PX010               BACK AND START AGAIN                 00213
               EJECT                                                    00214
*---------------------------------------------------------------------* 00215
*        END OF DATA ROUTINE FOR THE PTF INPUT FILE  - DDNAME         * 00216
*        IS INPUT.                                                    * 00217
*---------------------------------------------------------------------* 00218
EOF      EQU   *                                                        00219
         CLOSE SYSIN                                                    00220
         CLOSE SORT                                                     00221
         SPACE 3                                                        00222
*---------------------------------------------------------------------* 00223
*        LINK TO SORT (THIS WAS DEVELOPED USING SYNCSORT)             * 00224
*---------------------------------------------------------------------* 00225
         LA    R1,SORTPARM         GET ADDRESS OF SORT PARMS            00226
         LINK  EP=SORT             CALL THE SORT UTILITY                00227
         LTR   R15,R15             CHECK RETURN CODE                    00228
         BNZ   SORTERR             NO GOOD - ABEND                      00229
         EJECT                                                          00230
*---------------------------------------------------------------------* 00231
*        THIS IS THE BEGINNING OF PHASE 2 - SETS UP THE               * 00232
*        FILES, VERIFIES THE EXSISTANCE OF THE TITLES FILE, AND       * 00233
*        WRITES THE FIRST SET OF HEADERS                              * 00234
*---------------------------------------------------------------------* 00235
         OPEN  (SORT,INPUT)                                             00236
         OPEN  (TITLE,INPUT)                                            00237
         OPEN  (PRINT,OUTPUT)                                           00238
         LA    R5,TITLE            GET DCB ADDRESS                      00239
         TM    48(R5),X'10'        WAS OPEN SUCCESSFUL?                 00240
         BO    PX490               YES - BRANCH AROUND BAD OPEN         00241
         OI    FLAG,NOTITLE        SHOW NOT TO READ THIS FILE           00242
         WTO   'TITLE FILE OPEN UNSUCCESSFUL',ROUTCDE=(11),            X00243
               DESC=(7)                                                 00244
PX490    EQU   *                                                        00245
         BAL   R2,PXPUT            LINK TO HEADER PUT ROUTINE           00246
         EJECT                                                          00247
*---------------------------------------------------------------------* 00248
*        THIS SECTION FOOLS AROUND WITH THE SPECIAL HANDLING FOR      * 00249
*        THE 1ST RECORD.                                              * 00250
*---------------------------------------------------------------------* 00251
         GET   SORT,OUTPTF         1ST READ ONLY                        00252
         MVC   HOLD,OUTFMID        SAVE FMID FOR COMPARISON             00253
         MVC   OUTAREA+1(7),OUTFMID SETUP 1ST LINE                      00254
         LA    R5,OUTAREA+11       POINT TO FIRST PTF AREA              00255
         LA    R6,12               SET UP LOOP FOR BCT                  00256
         TM    FLAG,NOTITLE        CAN WE READ THE TITLE FILE?          00257
         BO    PX510               NO - WOOPS - FORGOT TO SAY           00258
*                                         MOTHER MAY I                  00259
         LA    R7,PX499            SAVE RETURN POINT FOR EODAD          00260
PX495    EQU   *                                                        00261
         GET   TITLE,INAREA        GET A TITLE RECORD                   00262
         CLC   INAREA+2(7),HOLD    DO FMID'S AGREE?                     00263
         BL    PX495               NO - LOWER, GO READ ANOTHER          00264
         BH    PX499               NO - HIGHER, ERGO NO BRANCH, SKIP    00265
         MVC   OUTAREA+21(34),INAREA+11 EQUAL - GET TITLE               00266
PX499    EQU   *                                                        00267
         PUT   PRINT,LINE          WRITE HEADER                         00268
         MVI   OUTAREA+0,C' '      CLEAR LINE                           00269
         MVC   OUTAREA+1(131),OUTAREA+0                                 00270
         AP    LINECNT,ONE         UPDATE LINE COUNT                    00271
*                       NOTE - PAGE OVERFLOW NOT POSSIBLE HERE          00272
         B     PX510               BRANCH AROUND NORMAL READ            00273
         EJECT                                                          00274
*---------------------------------------------------------------------* 00275
*        THIS IS THE MAINLINE ROUTINE TO PRINT OUT THE CROSS          * 00276
*        REFERENCE. IT READS THE SORTED INPUT, CHECKS TO SEE          * 00277
*        IF IT IS THE SAME AS THE PREVIOUS FMID, AND BRANCHES         * 00278
*        ACCORDINGLY.                                                 * 00279
*---------------------------------------------------------------------* 00280
PX500    EQU   *                                                        00281
         GET   SORT,OUTPTF         NORMAL READ                          00282
PX510    EQU   *                                                        00283
         CLC   HOLD,OUTFMID        SAME AS LAST RECORD?                 00284
         BNE   PX570               NO - GO WRITE PARTIAL LINE           00285
PX520    EQU   *                                                        00286
         MVC   0(7,R5),OUTPTF      MOVE IN PTF NUMBER                   00287
         LA    R5,10(R5)           BUMP TO NEXT PRINT POSITION          00288
         BCT   R6,PX500            LOOP IN HERE TOO.                    00289
         PUT   PRINT,LINE          END OF LOOP, WRITE LINE              00290
         MVI   OUTAREA+0,C' '      BLANK OUT THE LINE                   00291
         MVC   OUTAREA+1(131),OUTAREA CLEAR DATA LINE                   00292
         LA    R5,OUTAREA+11       POINT BACK TO BEGINNING              00293
         LA    R6,12               RESET LOOP CONTROL                   00294
         AP    LINECNT,ONE         ADD ONE TO CURRENT NUMBER OF LINES   00295
         CP    LINECNT,=P'60'      ENOUGH LINES FOR BREAK?              00296
         BL    PX500               NO - BACK TO TOP                     00297
         BAL   R2,PXPUT            YES - LINK TO HEADER PUT ROUTINE     00298
         B     PX500               BACK TO TOP                          00299
         SPACE 2                                                        00300
PX570    EQU   *                                                        00301
         PUT   PRINT,LINE          WRITE SHORT LINE                     00302
         PUT   PRINT,BLANK         WRITE SPACER                         00303
         AP    LINECNT,THREE       ADD IN LINE COUNT                    00304
         CP    LINECNT,=P'59'      ALMOST AT END OF PAGE?               00305
         BL    PX572               NO - BRANCH AROUND HDR ROUTINE       00306
         BAL   R2,PXPUT            OFF TO SEE THE WIZARD                00307
PX572    EQU   *                                                        00308
         LA    R5,11(R5)           RESET POINTER                        00309
         LA    R6,12               RESET BCT LOOP                       00310
         MVI   OUTAREA+0,C' '      CLEAR OUTPUT                         00311
         MVC   OUTAREA+1(131),OUTAREA                                   00312
         MVC   OUTAREA+1(7),OUTFMID SETUP 1ST LINE                      00313
         MVC   HOLD,OUTFMID        SAVE NEW FMID                        00314
         TM    FLAG,NOTITLE        CAN WE READ THE TITLE FILE?          00315
         BO    PX580               NO - FORGET THIS NONSENSE            00316
         LA    R7,PX580            YES - GET RETURN ADDRESS FOR EODAD   00317
         CLC   INAREA+2(7),HOLD    CURRENT ONE RIGHT?                   00318
         BL    PX575               NO - LOW, GO GET NEXT ONE            00319
         BH    PX580               NO - HIGH, STILL NOT RIGHT           00320
         MVC   OUTAREA+21(34),INAREA+11   YES - THIS IS RIGHT ONE       00321
         B     PX580               GO DIRECTLY TO PUT                   00322
PX575    EQU   *                                                        00323
         GET   TITLE,INAREA        READ THE FILE                        00324
         CLC   INAREA+2(7),HOLD    THIS THE RIGHT GUY?                  00325
         BL    PX575               NO - LOW, GO BACK AND READ AGAIN     00326
         BH    PX580               NO - HIGH, NONE FOR THIS ONEADDRESS  00327
         MVC   OUTAREA+21(34),INAREA+11  MOVE IN TITLE                  00328
PX580    EQU   *                                                        00329
         PUT   PRINT,LINE          WITE DATA                            00330
         MVI   OUTAREA+0,C' '      CLEAR OUTPUT                         00331
         MVC   OUTAREA+1(131),OUTAREA                                   00332
         LA    R5,OUTAREA+11       POINT BACK TO BEGINNING              00333
         LA    R6,12               RESET LOOP CONTROL                   00334
         AP    LINECNT,ONE         ADD TO CURRENT LINE COUNTER          00335
         CP    LINECNT,=P'60'      ENOUGH FOR A NEW PAGE?               00336
         BL    PX520               NO - BACK TO PROCESS NEW FMID        00337
         BAL   R2,PXPUT            YES - LINK TO HEADER PUT ROUTINE     00338
         B     PX520               BACK AND CONTINUE ON                 00339
         EJECT                                                          00340
*---------------------------------------------------------------------* 00341
*        EODAD ROUTINE FOR THE TITLE FILE. SINCE THIS CAN BE ENTERED  * 00342
*        FROM TWO PLACES, EACH SETS A RETURN ADDRESS, SO THAT         * 00343
*        PROCESSING CAN CONTINUE AFTER EOF IS REACHED.                * 00344
*---------------------------------------------------------------------* 00345
PX590    EQU   *                                                        00346
         OI    FLAG,NOTITLE        SHOW NO MORE READS TO BE DONE        00347
         BR    R7                  RETURN VIA PRESET REG 7              00348
         SPACE 3                                                        00349
*---------------------------------------------------------------------* 00350
*        EODAD FOR THE SORT FILE OUTPUT. WHEN YOU GET HERE ITS OVER.  * 00351
*---------------------------------------------------------------------* 00352
PX599    EQU   *                                                        00353
         PUT   PRINT,LINE          END OF LOOP, WRITE LINE              00354
         CLOSE SORT                                                     00355
         CLOSE PRINT                                                    00356
         L     R13,SAVE+4                                               00357
         LM    R14,R12,12(R13)                                          00358
         SR    R15,R15                                                  00359
         BR    R14                                                      00360
         EJECT                                                          00361
*---------------------------------------------------------------------* 00362
*        THIS IS THE COMMON READ ROUTINE FOR THE INPUT PTF FILE       * 00363
*        USED DURING PHASE ONE PROCESSING.                            * 00364
*---------------------------------------------------------------------* 00365
PXGET    EQU   *                   COMMON READ ROUTINE FOR SYSIN        00366
         GET   SYSIN,INAREA                                             00367
         LA    R3,72               SET UP REG 3 FOR BCT                 00368
         LA    R4,INAREA           POINT TO DATA                        00369
         BR    R2                  RETURN TO CALLER                     00370
         SPACE 2                                                        00371
*---------------------------------------------------------------------* 00372
*        THIS IS THE COMMON HEADER WRITTING ROUTINE.                  * 00373
*---------------------------------------------------------------------* 00374
PXPUT    EQU   *                                                        00375
         AP    PAGE,ONE            ADD TO PAGE NUMBER                   00376
         MVC   NUM,PATRN           INSERT EDIT PATTERN                  00377
         ED    NUM,PAGE            MAKE THE NUMBER PRINTABLE            00378
         PUT   PRINT,HDR1                                               00379
         PUT   PRINT,HDR2                                               00380
         PUT   PRINT,HDR3                                               00381
         ZAP   LINECNT,FOUR        RESET PAGE LINE COUNTER              00382
         BR    R2                  RETURN TO CALLER                     00383
         SPACE 2                                                        00384
*---------------------------------------------------------------------* 00385
*        IF THE SORT RETURNS A NON-ZERO CONDITION CODE,               * 00386
*        CONTROL IS PASSED TO THIS ABEND MACRO.                       * 00387
*---------------------------------------------------------------------* 00388
SORTERR  EQU   *                                                        00389
         ABEND 128,DUMP                                                 00390
         EJECT                                                          00391
         LTORG                                                          00392
         EJECT                                                          00393
SYSIN    DCB   DSORG=PS,MACRF=GM,EODAD=EOF,DDNAME=INPUT,RECFM=FB,      X00394
               LRECL=80                                                 00395
         EJECT                                                          00396
SORT     DCB   DSORG=PS,MACRF=(PM,GM),DDNAME=SORTIN,RECFM=FB,LRECL=15, X00397
               BLKSIZE=1500,EODAD=PX599                                 00398
         EJECT                                                          00399
PRINT    DCB   DSORG=PS,MACRF=PM,DDNAME=PRINTER,RECFM=VBA,LRECL=137,   X00400
               BLKSIZE=141                                              00401
         EJECT                                                          00402
TITLE    DCB   DSORG=PS,MACRF=GM,DDNAME=TITLES,RECFM=FB,               X00403
               LRECL=80,EODAD=PX590                                     00404
         EJECT                                                          00405
SAVE     DS    18F                                                      00406
HOLD     DS      CL7                                                    00407
PAGE     DC      PL2'0'                                                 00408
LINECNT  DC      PL2'0'                                                 00409
ONE      DC      PL1'1'                                                 00410
THREE    DC      PL1'3'                                                 00411
FOUR     DC      PL1'4'                                                 00412
FLAG     DC      XL1'00'                                                00413
NOTITLE  EQU     X'80'                                                  00414
PATRN    DC      XL4'40202120'                                          00415
INAREA   DS      CL80                                                   00416
OUTPTF   DC      CL8' '                                                 00417
OUTFMID  DC      CL7' '                                                 00418
         DS     0F                                                      00419
SORTPARM DC      X'80'                                                  00420
         DC      AL3(PARMLIST)                                          00421
         DC      H'0'                                                   00422
PARMLIST DC      AL2(PARMEND-PARMLIST)                                  00423
         DC      A(SORTCARD)                                            00424
         DC      A(SORTEND)                                             00425
         DC      A(RECCARD)                                             00426
         DC      A(RECEND)                                              00427
         DC      A(0)                                                   00428
         DC      A(0)                                                   00429
PARMEND  EQU   *                                                        00430
SORTCARD DC      C' SORT FIELDS=(9,7,CH,A,1,7,CH,A)'                    00431
SORTEND  DC      C' '                                                   00432
RECCARD  DC      C' RECORD LENGTH=15,TYPE=F'                            00433
RECEND   DC      C' '                                                   00434
HDR1     DC      H'133'                                                 00435
         DC      H'0'                                                   00436
         DC      CL1'1'                                                 00437
         DC    47CL1' '                                                 00438
         DC      C'PTF BY FMID CROSS REFERENCE'                         00439
         DC    46CL1' '                                                 00440
         DC      CL4'PAGE'                                              00441
NUM      DC      CL4'    '                                              00442
HDR2     DC      H'135'                                                 00443
         DC      H'0'                                                   00444
         DC      CL1'0'                                                 00445
         DC      CL10'FMID'                                             00446
         DC    12CL10'PTF'                                              00447
HDR3     DC      H'135'                                                 00448
         DC      H'0'                                                   00449
         DC      CL1' '                                                 00450
         DC    13CL10'*******'                                          00451
LINE     DC      H'137'                                                 00452
         DC      H'0'                                                   00453
OUTAREA  DC   133CL1' '                                                 00454
BLANK    DC      H'6'                                                   00455
         DC      H'0'                                                   00456
         DC      CL2'0 '                                                00457
         EJECT                                                          00458
*********************************************************************** 00459
*              REGISTER EQUIVALENTS                                   * 00460
*********************************************************************** 00461
R0       EQU   0                                                        00462
R1       EQU   1                                                        00463
R2       EQU   2                                                        00464
R3       EQU   3                                                        00465
R4       EQU   4                                                        00466
R5       EQU   5                                                        00467
R6       EQU   6                                                        00468
R7       EQU   7                                                        00469
R8       EQU   8                                                        00470
R9       EQU   9                                                        00471
R10      EQU   10                                                       00472
R11      EQU   11                                                       00473
R12      EQU   12                                                       00474
R13      EQU   13                                                       00475
R14      EQU   14                                                       00476
R15      EQU   15                                                       00477
         END                                                            00478
