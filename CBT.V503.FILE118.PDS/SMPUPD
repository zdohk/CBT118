* ----------------------------------------------------------------- *
*       SMPUPD     -   ADAPTED FROM A COBOL PROGRAM BY SAM GOLOB    *
* ----------------------------------------------------------------- *
*                                                                   *
*   SMPUPD - PROGRAM TO READ A SMPPTFIN FILE AND ADD ./ ADD NAME=   *
*            CARDS IN FRONT OF EVERY SYSMOD (PTF, APAR, USERMOD,    *
*            OR FUNCTION).  THIS ALLOWS YOU TO BREAK ANY SMPPTFIN   *
*            FILE (PUT TAPE, ETC.) INTO A PDS.  THIS PROGRAM WILL   *
*            ALSO CHANGE ./ CHANGE CARDS AND OTHER ./ CARDS, WHICH  *
*            ARE INTERNAL TO EACH SYSMOD, INTO >< CARDS.  THIS      *
*            ALLOWS THE PDSLOAD PROGRAM (FROM CBT TAPE FILE 093)    *
*            TO PROPERLY RECONSTITUTE ALL SYSMODS AS MEMBERS OF     *
*            THE PDS.                                               *
*                                                                   *
*                                                                   *
*   LEVEL 005 - PARM=READ WILL GIVE READ ONLY OPERATION AND WILL    *
*   (06/21/01)  ONLY REQUIRE SYSUT1 AND SYSPRINT DD NAMES.  THE     *
*               PURPOSE OF THE PARM=READ RUN, IS TO PRINT STATS     *
*               ABOUT THE SMPPTFIN FILE.                            *
*                                                                   *
*   LEVEL 004 - ENHANCED SYSPRINT AND SUMMARY INFORMATION ABOUT     *
*   (06/17/01)  ALL SYSMODS THAT WERE READ INTO SYSUT1.             *
*                                                                   *
* ----------------------------------------------------------------- *
*    SET LINES PER PAGE VARIABLE, FOR PRINT CONTROL.
*    AND SET GLOBAL VARIABLES.
*
         GBLA  &LINEPPG,&LINEINT
&LINEPPG SETA  60
&LINEINT SETA  &LINEPPG+1
         GBLC  &LLEV
&LLEV    SETC  '005'
* ----------------------------------------------------------------- *
SMPUPD   CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'SMPUPD - PUTXREF REPORT TO FORFMID LIST OF PTFS - '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
         DC    C'LEVEL &LLEV '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP THREE BASE REGISTERS
         USING SMPUPD,R12,R11,R10
         LR    R12,R15
         LR    R11,R12
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LA    R10,4095(,R11)
         LA    R10,1(,R10)
         LR    R1,R5          RESTORE PARM POINTER
         L     R1,0(R1)       GET PARM FROM JCL
         MVC   READST(4),2(R1)
         OC    READST(4),=X'40404040'
         CLC   =C'READ',READST  IS THIS RUN READ ONLY?
         BNE   NOTREADO
         OI    FLAG2,X'80'     FLAG THIS RUN AS READ ONLY
         MVC   HEADER+69(11),READONL    ADD READ ONLY TO HEADER
NOTREADO DS    0H
         PRINT GEN
         OPEN  (SYSUT1,INPUT)
         OPEN  (SYSPRINT,OUTPUT)
         TM    FLAG2,X'80'           IS ROUTINE READ ONLY?
         BO    INITIT
         OPEN  (SYSUT2,OUTPUT)
INITIT   DS    0H                   INITIALIZE AREAS AND COUNTERS
         MVC   BLANKS,BLANKS-1
         MVC   INAREA,INAREA-1
         MVC   OUTRECD,OUTRECD-1
         MVC   UPDTELIN,UPDTELIN-1
         MVC   UPDTELT,UPDTELIT
         ZAP   INRCDS,=P'0'
         ZAP   OUTRCDS,=P'0'
         BAL   R9,ZEROTOTS
         BAL   R9,ZEROSUBS
         ZAP   LINECNT,=P'&LINEINT'   FORCE PRINT OF HEADER
         ZAP   PAGECNT,=P'0'        INITIALIZE PAGE COUNTERS
         MVI   FLAG,X'02'           MARK AS FIRST SYSMOD BEING READ
***********************************************************************
* GET SYSTEM DATE AND TIME                                            *
***********************************************************************
         TIME  DEC,DATETIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY
         OI    DATE+4,X'0F'
         UNPK  DATED,DATE
         OI    TIME+3,X'0F'
         UNPK  TIMED,TIME
*                          FILL HEADDATE AREA WITH INFO TO REUSE
         MVC   HEADDATE(2),DATED
         MVI   HEADDATE+2,C'/'
         MVC   HEADDATE+3(2),DATED+2
         MVI   HEADDATE+5,C'/'
         MVC   HEADDATE+6(4),DATED+4
         MVC   HEADDATE+12(2),TIMED
         MVI   HEADDATE+14,C':'
         MVC   HEADDATE+15(2),TIMED+2
         MVI   HEADDATE+17,C':'
         MVC   HEADDATE+18(2),TIMED+4
         MVC   HEADDAY(2),DATED
         MVI   HEADDAY+2,C'/'
         MVC   HEADDAY+3(2),DATED+2
         MVI   HEADDAY+5,C'/'
         MVC   HEADDAY+6(2),DATED+6
***********************************************************************
         BAL   R9,DOHEDR
***********************************************************************
* -------                                MAIN PROCESSING LOOP
INREPT   DS    0H
         GET   SYSUT1,INAREA
         AP    TOTALRCD,=P'1'            COUNT RECORDS READ
         AP    SUBTORCD,=P'1'
         CLC   INCNTL(2),=C'./'
         BE    INUPDREP
         CLC   INCNTL(2),=C'++'
         BE    INPLUSPL
         BAL   R9,WRITEOUT               WRITE OUT CURRENT RECORD
         B     INREPT
INUPDREP DS    0H
         AP    TOTALCHG,=P'1'            COUNT RECORDS CHANGED
         AP    SUBTOCHG,=P'1'
         MVC   INCNTL(2),=C'><'
         BAL   R9,WRITEOUT               WRITE OUT CURRENT RECORD
         B     INREPT
INPLUSPL DS    0H
         BAL   R9,FINDKEWD               DETERMINE KEYWORD OF PLUSPLUS
         CLC   KEYWD,=C'        '        WAS ONE OF THE PROPER ONES?
         BNE   INGETNUM                  YES - GET THE SYSMOD NUMBER
         BAL   R9,WRITEOUT               WRITE OUT CURRENT RECORD
         B     INREPT
INGETNUM BAL   R9,FINDNUMB               GET SYSMOD NUMBER
         CLC   PTFNAM,=C'       '        DID WE GET ONE?
         BE    INGETWRT                  NO-NO UPDTE WRITE
         MVC   UPDTENAM(7),PTFNAM        MOVE IN PTF NAME TO UPDTE
         TM    FLAG,X'02'                FIRST SYSMOD?
         BZ    INGET1ST                  NO. DON'T COMPENSATE COUNT
         SP    SUBTORCD,=P'1'
INGET1ST DS    0H
         TM    FLAG,X'02'                FIRST SYSMOD?
         BO    INGETUPD                  YES. NO SUBTOTALS.
         BAL   R9,MAKERMSG
         AP    LINECNT,=P'1'
         ST    R9,SAVER9                 CHECK FOR HEADER PRINT
         BAL   R9,DOHEDR
         L     R9,SAVER9
         PUT   SYSPRINT,AREAOUT
         BAL   R9,ZEROSUBS               ZERO COUNTERS FOR NEXT TIME
INGETUPD BAL   R9,WRITEUPD               WRITE UPDTE RECORD BUILT
*                                        PUT TOTALS HERE LATER
         MVC   UPDTELIN,UPDTELIN-1       CLEAR UPDTE AREA
         MVC   UPDTELT,UPDTELIT          AND SET UP FOR NEXT TIME
INGETWRT BAL   R9,WRITEOUT               WRITE OUT CURRENT RECORD
         B     INREPT
* -------
EOFIN    DS    0H                        CLOSE FILES AT END OF INPUT
         CLOSE SYSUT1
         TM    FLAG2,X'80'                IS ROUTINE READ ONLY?
         BO    THEEND                    YES. SYSUT2 IS NOT OPEN.
         CLOSE SYSUT2
* -------
THEEND   DS    0H                        GET OUT OF PROGRAM
         AP    SUBTORCD,=P'1'         COMPENSATE FOR RCD READ EARLIER
         BAL   R9,MAKERMSG               PUT OUT LAST MESSAGE
         AP    LINECNT,=P'1'
         ST    R9,SAVER9                 CHECK FOR HEADER PRINT
         BAL   R9,DOHEDR
         L     R9,SAVER9
         PUT   SYSPRINT,AREAOUT
         AP    TOTALSYM,TOTALFUN
         AP    TOTALSYM,TOTALAPA
         AP    TOTALSYM,TOTALPTF
         AP    TOTALSYM,TOTALUSE
         BAL   R9,MAKEFMSG               PUT OUT SUMMARY MESSAGES
         AP    TOTALTOT,TOTALRCD
         AP    TOTALTOT,TOTALCHG
*                                        PUT OUT SUMMARY MESSAGES HERE
         CLOSE SYSPRINT
         L     R13,4(,R13)
         RETURN (14,12),RC=0
         EJECT
* -------     SUBROUTINES    -------- *
ZEROTOTS DS    0H                        ZERO COUNTERS FOR TOTALS
         ZAP   TOTALTOT,=P'0'
         ZAP   TOTALADD,=P'0'
         ZAP   TOTALCHG,=P'0'
         ZAP   TOTALRCD,=P'0'
         ZAP   TOTALFUN,=P'0'
         ZAP   TOTALAPA,=P'0'
         ZAP   TOTALPTF,=P'0'
         ZAP   TOTALUSE,=P'0'
         BR    R9
* -------
ZEROSUBS DS    0H                        ZERO COUNTERS FOR NEW SYSMOD
         ZAP   SUBTOCHG,=P'0'
         ZAP   SUBTORCD,=P'0'
         ZAP   SUBTOFUN,=P'0'
         ZAP   SUBTOAPA,=P'0'
         ZAP   SUBTOPTF,=P'0'
         ZAP   SUBTOUSE,=P'0'
         BR    R9
* -------
MAKEFMSG DS    0H                        MAKE FINAL SUMMARY REPORT
         AP    PAGECNT,=P'1'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADERF
         MVC   OUTLINE+95(23),HEADDATE
         MVC   OUTLINE+120(12),EDPAT
         ED    OUTLINE+120(12),PAGECNT+2
         MVC   OUTLINE+120(4),=C'PAGE'
         PUT   SYSPRINT,OUTLINE
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,TOTPTMSG
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTALRCD+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,TOTADMSG
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTALADD+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,TOTOTMSG
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTALOUT+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSTADDCH
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTALCHG+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSTSYSMD
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTALSYM+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSTFUNCT
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTALFUN+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSTPTF
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTALPTF+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSTAPAR
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTALAPA+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSTUSERM
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTALUSE+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSRFUNCT
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTRCFUN+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSRPTF
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTRCPTF+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSRAPAR
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTRCAPA+2
         PUT   SYSPRINT,AREAOUT
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSRUSERM
         MVC   AREAOUT+37(12),EDPAT
         ED    AREAOUT+37(12),TOTRCUSE+2
         PUT   SYSPRINT,AREAOUT
         BR    R9
* -------
MAKERMSG DS    0H                        MAKE MESSAGE FOR EACH SYSMOD
         MVC   AREAOUT,AREAOUT-1
         MVC   AREAOUT,MSGRECDS
         MVC   AREAOUT+1(8),KEYWDB       MOVE IN SYSMOD TYPE
*
* ----       INCREMENT LINE COUNTERS BY TYPE OF SYSMOD    ------ *
*
INCRTPE  DS    0H
         CLC   =C'PTF',KEYWDB
         BE    INCRPTF
         CLC   =C'APAR',KEYWDB
         BE    INCRAPA
         CLC   =C'FUNCTION',KEYWDB
         BE    INCRFUN
         CLC   =C'USERMOD',KEYWDB
         BE    INCRUSE
         B     INCRTPEN
INCRPTF  DS    0H
         AP    TOTRCPTF,SUBTORCD
         B     INCRTPEN
INCRAPA  DS    0H
         AP    TOTRCAPA,SUBTORCD
         B     INCRTPEN
INCRFUN  DS    0H
         AP    TOTRCFUN,SUBTORCD
         B     INCRTPEN
INCRUSE  DS    0H
         AP    TOTRCUSE,SUBTORCD
         B     INCRTPEN
INCRTPEN DS    0H
*
* ----       INCREMENT LINE COUNTERS BY TYPE OF SYSMOD    ------ *
*
         MVC   AREAOUT+10(7),PTFNAMB     MOVE IN SYSMOD NAME
         MVC   AREAOUT+22(12),EDPAT
         ED    AREAOUT+22(12),SUBTORCD+2
         MVC   AREAOUT+48(12),EDPAT
         ED    AREAOUT+48(12),SUBTOCHG+2
         BR    R9
* -------
SYSPROUT DS    0H                        WRITE PRINT RECORD
         AP    LINECNT,=P'1'
         ST    R9,SAVER9                 CHECK FOR HEADER
         BAL   R9,DOHEDR
         L     R9,SAVER9
         PUT   SYSPRINT,AREAOUT
         BR    R9
* -------
WRITEOUT DS    0H                        WRITE RECORD FROM INPUT AREA
         TM    FLAG2,X'80'                IS RUN READ ONLY?
         BO    WRITEOUE                  YES, THEN NOP THIS ROUTINE
         AP    TOTALOUT,=P'1'
         PUT   SYSUT2,INAREA
WRITEOUE BR    R9
* -------
WRITEUPD DS    0H                        WRITE RECORD FROM UPDTE AREA
         NI    FLAG,X'FF'-X'02'          TURN OFF INITIAL RECD FLAG
         OI    FLAG,X'01'                SHOW WE'VE ALREADY DONE ONE
         TM    FLAG2,X'80'                IS RUN READ ONLY?
         BO    WRITEBKP                  YES, THEN AVOID WRITE
         AP    TOTALOUT,=P'1'
         PUT   SYSUT2,UPDTELIN
         AP    TOTALADD,=P'1'
WRITEBKP TM    FLAG,X'02'
         BO    WRITEUPE                  DON'T BACK UP THE FIRST TIME
         MVC   KEYWDB,KEYWD              BACK UP SYSMOD TYPE
         MVC   PTFNAMB,PTFNAM            BACK UP SYSMOD NAME
WRITEUPE BR    R9
* -------
FINDKEWD DS    0H
         MVC   KEYWD,KEYWD-1             CLEAR KEYWORD STORAGE AREA
         LA    R7,INAREA+2               START POINT
         LA    R8,INAREA+69              END POINT
FINDLOOP DS    0H
         CR    R7,R8                     PAST THE END?
         BNL   FINDLNUN                  DIDN'T FIND A KEYWORD
*
*  ---                   CHECK FOR COMMENTS DURING LINE SEARCH
*
         CLC   SLAST(2),0(R7)
         BE    COMMSTRT
         CLC   ASTSL(2),0(R7)
         BE    COMMSTOP
         B     COMMCONT
COMMSTRT DS    0H
         LA    R7,2(,R7)                 BUMP PAST COMMENT
         OI    FLAG,X'20'                INDICATE COMMENT AREA HERE
         B     FINDLOOP                  CONTINUE LOOPING
COMMSTOP DS    0H
         LA    R7,2(,R7)                 BUMP PAST COMMENT ENDER
         NI    FLAG,X'FF'-X'20'          NO MORE COMMENT HERE
         B     FINDLOOP
COMMCONT DS    0H
         TM    FLAG,X'20'
         BZ    COMMGO1
         LA    R7,1(,R7)                 BUMP ONE SPACE
         B     FINDLOOP                  IGNORE EVERYTHING IF COMMENT
COMMGO1  DS    0H                        FIND FIRST NON-BLANK
         CLI   0(R7),X'40'
         BNE   COMMGO2                   NOT BLANK, SEARCH FOR KEYWD
         LA    R7,1(,R7)                 IS BLANK, BUMP AND TRY AGAIN
         B     FINDLOOP
COMMGO2  DS    0H
*
*  ---                   CHECK FOR COMMENTS DURING LINE SEARCH
*
         CLC   =C'PTF',0(R7)
         BE    FOUNDPTF
         CLC   =C'APAR',0(R7)
         BE    FOUNDAPA
         CLC   =C'FUNCTION',0(R7)
         BE    FOUNDFUN
         CLC   =C'USERMOD',0(R7)
         BE    FOUNDUSR
         B     FINDLSTP
FOUNDPTF DS    0H
         MVC   KEYWD(3),=C'PTF'
         TM    FLAG,X'02'
         BZ    FOUNDPT1
         MVC   KEYWDB(8),KEYWD           INITIALIZE BKUP FIRST TIME
FOUNDPT1 DS    0H
         AP    SUBTOPTF,=P'1'            COUNT PTF      FOUND
         AP    TOTALPTF,=P'1'
         LA    R7,3(,R7)
         B     FINDKEND
FOUNDAPA DS    0H
         MVC   KEYWD(4),=C'APAR'
         TM    FLAG,X'02'
         BZ    FOUNDAP1
         MVC   KEYWDB(8),KEYWD           INITIALIZE BKUP FIRST TIME
FOUNDAP1 DS    0H
         AP    SUBTOAPA,=P'1'            COUNT APAR     FOUND
         AP    TOTALAPA,=P'1'
         LA    R7,4(,R7)
         B     FINDKEND
FOUNDFUN DS    0H
         MVC   KEYWD(8),=C'FUNCTION'
         TM    FLAG,X'02'
         BZ    FOUNDFU1
         MVC   KEYWDB(8),KEYWD           INITIALIZE BKUP FIRST TIME
FOUNDFU1 DS    0H
         AP    SUBTOFUN,=P'1'            COUNT FUNCTION FOUND
         AP    TOTALFUN,=P'1'
         LA    R7,8(,R7)
         B     FINDKEND
FOUNDUSR DS    0H
         MVC   KEYWD(7),=C'USERMOD'
         TM    FLAG,X'02'
         BZ    FOUNDUS1
         MVC   KEYWDB(8),KEYWD           INITIALIZE BKUP FIRST TIME
FOUNDUS1 DS    0H
         AP    SUBTOUSE,=P'1'            COUNT USERMOD  FOUND
         AP    TOTALUSE,=P'1'
         LA    R7,7(,R7)
         B     FINDKEND
FINDLSTP DS    0H
         LR    R7,R8
         LA    R7,1(,R7)
         MVC   KEYWD,KEYWD-1
         B     FINDKEND
FINDLNUN DS    0H
         MVC   KEYWD,KEYWD-1
         LA    R7,INAREA+2               RE-INITIALIZE POINTER
         B     FINDKEND
FINDKEND BR    R9
* -------
FINDNUMB DS    0H
*                                        R7 ASSUMED SET BY FINDKEWD
         LA    R8,INAREA+64              END POINT
FINDNLUP DS    0H
         CR    R7,R8                     PAST THE END?
         BNL   FINDNNUN                  DIDN'T FIND A SYSMOD NUMBER
*
*  ---                   CHECK FOR COMMENTS DURING LINE SEARCH
*
         CLC   SLAST(2),0(R7)
         BE    COMFSTRT
         CLC   ASTSL(2),0(R7)
         BE    COMFSTOP
         B     COMFCONT
COMFSTRT DS    0H
         LA    R7,2(,R7)                 BUMP PAST COMMENT
         OI    FLAG,X'20'                INDICATE COMMENT AREA HERE
         B     FINDNLUP                  CONTINUE LOOPING
COMFSTOP DS    0H
         LA    R7,2(,R7)                 BUMP PAST COMMENT ENDER
         NI    FLAG,X'FF'-X'20'          NO MORE COMMENT HERE
         B     FINDNLUP                  CONTINUE LOOPING
COMFCONT DS    0H
         TM    FLAG,X'20'                IN MIDDLE OF COMMENT?
         BZ    COMFGO1                   NO. PROCESS NORMALLY.
         LA    R7,1(,R7)                 BUMP ONE SPACE
         B     FINDNLUP                  IGNORE EVERYTHING IF COMMENT
COMFGO1  DS    0H                        FIND FIRST NON-BLANK
*
*  ---                   CHECK FOR COMMENTS DURING LINE SEARCH
*
         CLI   0(R7),C' '                CURRENT CHARACTER A SPACE?
         BE    FINDNBMP                  YES-THEN GO PAST AND RETRY
         CLI   0(R7),C'('                OPEN PAREN?
         BE    FINDNGET                  YES-TRY TO GET SYSMOD NUMBER
FINDNBMP DS    0H
         LA    R7,1(,R7)                 BUMP POINTER
         B     FINDNLUP                  AND RETRY
FINDNGET DS    0H
         CR    R7,R8                     PAST THE END?
         BNL   FINDNNUN                  DIDN'T FIND A SYSMOD NUMBER
         CLI   1(R7),C' '                NEXT CHAR IS SPACE?
         BE    FINDNGBM                  YES-BUMP AND RETRY
         MVC   PTFNAM(7),1(R7)           ASSUME NEXT 7 CHARS ARE NUMBER
         TM    FLAG,X'02'
         BZ    FINDNGE1
         MVC   PTFNAMB,PTFNAM            LOAD BACKUP THE FIRST TIME
FINDNGE1 DS    0H
         LA    R7,INAREA+2               RESET POINTER
         B     FINDNEND                  AND GET OUT SUCCESSFULLY
FINDNGBM DS    0H
         LA    R7,1(,R7)                 BUMP POINTER
         B     FINDNGET                  AND RETRY SECOND LOOP
FINDNNUN DS    0H
         MVC   PTFNAM,PTFNAM-1
         LA    R7,INAREA+2               RE-INITIALIZE POINTER
         B     FINDNEND
FINDNEND BR    R9
* -------
DOHEDR   DS    0H                        PRINT THE PAGE HEADER
         CP    LINECNT,=P'&LINEPPG'
         BL    DOHEDRE
         AP    PAGECNT,=P'1'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER
         MVC   OUTLINE+95(23),HEADDATE
         MVC   OUTLINE+120(12),EDPAT
         ED    OUTLINE+120(12),PAGECNT+2
         MVC   OUTLINE+120(4),=C'PAGE'
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVI   OUTLINE,C'-'
         PUT   SYSPRINT,OUTLINE
         ZAP   LINECNT,=P'4'
DOHEDRE  BR    R9
* -------
*
*
* ----- WORK AREA ------ *
READST   DC    CL4' '
SAVER9   DC    F'0'
SAVER9A  DC    F'0'
SAVER9B  DC    F'0'
FLAG     DC    X'00'
FLAG2    DC    X'00'
INRCDS   DC    PL7'0'
OUTRCDS  DC    PL7'0'
LINECNT  DC    PL7'0'
PAGECNT  DC    PL7'0'
TOTALTOT DC    PL7'0'
TOTALOUT DC    PL7'0'
TOTALADD DC    PL7'0'
TOTALCHG DC    PL7'0'
TOTALRCD DC    PL7'0'
TOTALSYM DC    PL7'0'
TOTALFUN DC    PL7'0'
TOTALAPA DC    PL7'0'
TOTALPTF DC    PL7'0'
TOTALUSE DC    PL7'0'
TOTRCFUN DC    PL7'0'
TOTRCAPA DC    PL7'0'
TOTRCPTF DC    PL7'0'
TOTRCUSE DC    PL7'0'
SUBTOCHG DC    PL7'0'
SUBTORCD DC    PL7'0'
SUBTOFUN DC    PL7'0'
SUBTOAPA DC    PL7'0'
SUBTOPTF DC    PL7'0'
SUBTOUSE DC    PL7'0'
         DC    CL4' '
KEYWD    DC    CL8' '
         DC    CL4' '
FMIDSTOR DC    CL7' '
         DC    CL4' '
KEYWDB   DC    CL8' '
         DC    CL4' '
SND7STOR DC    CL7' '
         DC    CL4' '
PTFNAM   DC    CL7' '
         DC    CL4' '
PTFNAMB  DC    CL7' '
         DC    CL4' '
         DC    CL1' '
AREAOUT  DS    0CL133
AREACC   DC    CL1' '
AREAREST DC    CL132' '
         DC    CL1' '
OUTLINE  DS    0CL133
OUTLCC   DC    CL1' '
OUTLINER DC    CL132' '
         DC    CL1' '
INAREA   DS    0CL80
INCNTL   DC    CL2' '
INREST   DC    CL78' '
         DC    CL8' '
         DC    C' '
UPDTELIN DS    0CL80              IEBUPDTE ADD CARD
UPDTELT  DC    CL12' '
UPDTENAM DC    CL8' '
UPDTERST DC    CL60' '
         DC    CL8' '
OUTRECD  DS    0CL80
OUTCNTL  DC    CL2' '
OUTREST  DC    CL78' '
         DC    CL8' '
* ----- CONSTANTS ------ *
UPDTELIT DC    C'./ ADD NAME='
TOTPTMSG DC    CL133'-        TOTAL RECORDS IN SMPPTFIN ='
TOTADMSG DC    CL133'         ./ ADD NAME RECORDS ADDED ='
TOTOTMSG DC    CL133'  TOTAL RECORDS PUT TO OUTPUT FILE ='
MSTADDCH DC    CL133'  TOTAL NUMBER OF ./ CHANGED TO >< ='
MSTSYSMD DC    CL133'  TOTAL NUMBER OF SYSMODS          ='
MSTFUNCT DC    CL133'0 TOTAL NUMBER OF FUNCTION SYSMODS ='
MSTPTF   DC    CL133'  TOTAL NUMBER OF PTF      SYSMODS ='
MSTAPAR  DC    CL133'  TOTAL NUMBER OF APAR     SYSMODS ='
MSTUSERM DC    CL133'  TOTAL NUMBER OF USERMOD  SYSMODS ='
MSRFUNCT DC    CL133'0 TOTAL NUMBER OF FUNCTION RECORDS ='
MSRPTF   DC    CL133'  TOTAL NUMBER OF PTF      RECORDS ='
MSRAPAR  DC    CL133'  TOTAL NUMBER OF APAR     RECORDS ='
MSRUSERM DC    CL133'  TOTAL NUMBER OF USERMOD  RECORDS ='
MSGRECDS DC    CL133' XXXXXXXX YYYYYYY HAS              RECORDS WITH   -
                           INTERNAL ./ RECORDS CHANGED TO ><'
SLAST    DC    CL2'/*'
ASTSL    DC    CL2'*/'
HEADER   DC    CL133'1        SMPUPD  -  ADD PDSLOAD (IEBUPDTE) CARDS T-
               O AN SMPPTFIN FILE'
HEADERF  DC    CL133'1        SMPUPD  -  ADD PDSLOAD (IEBUPDTE) CARDS T-
               O AN SMPPTFIN FILE - SUMMARY'
ONE      DC    F'1'
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
         DC    C' '
READONL  DC    CL11'- READ ONLY'
         LTORG
TIMED    DS    CL7
DATED    DS    CL9
DATETIME DS    4F
         ORG   DATETIME
TIME     DS    CL4
         DS    CL4
DATE     DS    CL5
         DS    CL3
HEADDATE DC    CL23' '
HEADDAY  DC    CL8' '
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GM,EODAD=EOFIN,DSORG=PS,LRECL=80
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,LRECL=80
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
         END   SMPUPD
