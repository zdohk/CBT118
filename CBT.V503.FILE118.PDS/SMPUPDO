* ----------------------------------------------------------------- *
*       SMPUPD     -   ADAPTED FROM A COBOL PROGRAM BY SAM GOLOB    *
* ----------------------------------------------------------------- *
         GBLC  &LLEV
&LLEV    SETC  '001'
SMPUPD   CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'SMPUPD - PUTXREF REPORT TO FORFMID LIST OF PTFS - '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
         DC    C'LEVEL &LLEV '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP THREE BASE REGISTERS
         USING SMPUPD,R12,R11,R10
         LR    R12,R15
         LR    R11,R12
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LA    R10,4095(,R11)
         LA    R10,1(,R10)
         LR    R1,R5          RESTORE PARM POINTER
         PRINT GEN
         OPEN  (SYSUT1,INPUT)
         OPEN  (SYSUT2,OUTPUT)
INITIT   DS    0H
         MVC   BLANKS,BLANKS-1
         MVC   INAREA,INAREA-1
         MVC   OUTAREA,OUTAREA-1
         MVC   UPDTELIN,UPDTELIN-1
         MVC   UPDTELT,UPDTELIT
***********************************************************************
* GET SYSTEM DATE AND TIME                                            *
***********************************************************************
         TIME  DEC,DATETIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY
         OI    DATE+4,X'0F'
         UNPK  DATED,DATE
         OI    TIME+3,X'0F'
         UNPK  TIMED,TIME
*                          FILL HEADDATE AREA WITH INFO TO REUSE
         MVC   HEADDATE(2),DATED
         MVI   HEADDATE+2,C'/'
         MVC   HEADDATE+3(2),DATED+2
         MVI   HEADDATE+5,C'/'
         MVC   HEADDATE+6(4),DATED+4
         MVC   HEADDATE+12(2),TIMED
         MVI   HEADDATE+14,C':'
         MVC   HEADDATE+15(2),TIMED+2
         MVI   HEADDATE+17,C':'
         MVC   HEADDATE+18(2),TIMED+4
         MVC   HEADDAY(2),DATED
         MVI   HEADDAY+2,C'/'
         MVC   HEADDAY+3(2),DATED+2
         MVI   HEADDAY+5,C'/'
         MVC   HEADDAY+6(2),DATED+6
***********************************************************************
* -------                                MAIN PROCESSING LOOP
INREPT   DS    0H
         GET   SYSUT1,INAREA
         CLC   INCNTL(2),=C'./'
         BE    INUPDREP
         CLC   INCNTL(2),=C'++'
         BE    INPLUSPL
         BAL   R9,WRITEOUT               WRITE OUT CURRENT RECORD
         B     INREPT
INUPDREP DS    0H
         MVC   INCNTL(2),=C'><'
         BAL   R9,WRITEOUT               WRITE OUT CURRENT RECORD
         B     INREPT
INPLUSPL DS    0H
         BAL   R9,FINDKEWD               DETERMINE KEYWORD OF PLUSPLUS
         CLC   KEYWD,=C'        '        WAS ONE OF THE PROPER ONES?
         BNE   INGETNUM                  YES - GET THE SYSMOD NUMBER
         BAL   R9,WRITEOUT               WRITE OUT CURRENT RECORD
         B     INREPT
INGETNUM BAL   R9,FINDNUMB               GET SYSMOD NUMBER
         CLC   PTFNAM,=C'       '        DID WE GET ONE?
         BE    INGETWRT                  NO-NO UPDTE WRITE
         MVC   UPDTENAM(7),PTFNAM        MOVE IN PTF NAME TO UPDTE
         BAL   R9,WRITEUPD               WRITE UPDTE RECORD BUILT
*                                        PUT TOTALS HERE LATER
         MVC   UPDTELIN,UPDTELIN-1       CLEAR UPDTE AREA
         MVC   UPDTELT,UPDTELIT          AND SET UP FOR NEXT TIME
INGETWRT BAL   R9,WRITEOUT               WRITE OUT CURRENT RECORD
         B     INREPT
* -------
EOFIN    DS    0H                        CLOSE FILES AT END OF INPUT
         CLOSE SYSUT1
         CLOSE SYSUT2
* -------
THEEND   DS    0H                        GET OUT OF PROGRAM
         CLOSE SYSPRINT
         L     R13,4(,R13)
         RETURN (14,12),RC=0
         EJECT
* -------     SUBROUTINES    -------- *
WRITEOUT DS    0H
         PUT   SYSUT2,INAREA             WRITE RECORD FROM INPUT AREA
         BR    R9
* -------
WRITEUPD DS    0H
         PUT   SYSUT2,UPDTELIN           WRITE RECORD FROM UPDTE AREA
         BR    R9
* -------
FINDKEWD DS    0H
         MVC   KEYWD,KEYWD-1             CLEAR KEYWORD STORAGE AREA
         LA    R7,INAREA+2               START POINT
         LA    R8,INAREA+69              END POINT
FINDLOOP DS    0H
         CR    R7,R8                     PAST THE END?
         BNL   FINDLNUN                  DIDN'T FIND A KEYWORD
         CLC   =C'PTF',0(R7)
         BE    FOUNDPTF
         CLC   =C'APAR',0(R7)
         BE    FOUNDAPA
         CLC   =C'FUNCTION',0(R7)
         BE    FOUNDFUN
         CLC   =C'USERMOD',0(R7)
         BE    FOUNDUSR
         LA    R7,1(,R7)                 BUMP POSITION POINTER
         B     FINDLOOP
FOUNDPTF DS    0H
         MVC   KEYWD(3),=C'PTF'
         LA    R7,3(,R7)
         B     FINDKEND
FOUNDAPA DS    0H
         MVC   KEYWD(4),=C'APAR'
         LA    R7,4(,R7)
         B     FINDKEND
FOUNDFUN DS    0H
         MVC   KEYWD(8),=C'FUNCTION'
         LA    R7,8(,R7)
         B     FINDKEND
FOUNDUSR DS    0H
         MVC   KEYWD(7),=C'USERMOD'
         LA    R7,7(,R7)
         B     FINDKEND
FINDLNUN DS    0H
         MVC   KEYWD,KEYWD-1
         LA    R7,INAREA+2               RE-INITIALIZE POINTER
         B     FINDKEND
FINDKEND BR    R9
* -------
FINDNUMB DS    0H
*                                        R7 ASSUMED SET BY FINDKEWD
         LA    R8,INAREA+64              END POINT
FINDNLUP DS    0H
         CR    R7,R8                     PAST THE END?
         BNL   FINDNNUN                  DIDN'T FIND A SYSMOD NUMBER
         CLI   0(R7),C' '                CURRENT CHARACTER A SPACE?
         BE    FINDNBMP                  YES-THEN GO PAST AND RETRY
         CLI   0(R7),C'('                OPEN PAREN?
         BE    FINDNGET                  YES-TRY TO GET SYSMOD NUMBER
FINDNBMP DS    0H
         LA    R7,1(,R7)                 BUMP POINTER
         B     FINDNLUP                  AND RETRY
FINDNGET DS    0H
         CR    R7,R8                     PAST THE END?
         BNL   FINDNNUN                  DIDN'T FIND A SYSMOD NUMBER
         CLI   1(R7),C' '                NEXT CHAR IS SPACE?
         BE    FINDNGBM                  YES-BUMP AND RETRY
         MVC   PTFNAM(7),1(R7)           ASSUME NEXT 7 CHARS ARE NUMBER
         LA    R7,INAREA+2               RESET POINTER
         B     FINDNEND                  AND GET OUT SUCCESSFULLY
FINDNGBM DS    0H
         LA    R7,1(,R7)                 BUMP POINTER
         B     FINDNGET                  AND RETRY SECOND LOOP
FINDNNUN DS    0H
         MVC   PTFNAM,PTFNAM-1
         LA    R7,INAREA+2               RE-INITIALIZE POINTER
         B     FINDNEND
FINDNEND BR    R9
* -------
*
*
* ----- WORK AREA ------ *
FLAG1    DC    X'00'
FLAG2    DC    X'00'
         DC    CL4' '
KEYWD    DC    CL8' '
         DC    CL4' '
FMIDSTOR DC    CL7' '
         DC    CL4' '
SND7STOR DC    CL7' '
         DC    CL4' '
PTFNAM   DC    CL7' '
         DC    CL4' '
         DC    CL1' '
INAREA   DS    0CL80
INCNTL   DC    CL2' '
INREST   DC    CL78' '
         DC    CL8' '
         DC    C' '
UPDTELIN DS    0CL80              IEBUPDTE ADD CARD
UPDTELT  DC    CL14' '
UPDTENAM DC    CL8' '
UPDTERST DC    CL58' '
         DC    CL8' '
OUTRECD  DS    0CL80
OUTCNTL  DC    CL2' '
OUTREST  DC    CL78' '
         DC    CL8' '
* ----- CONSTANTS ------ *
UPDTELIT DC    C'./  ADD  NAME='
SLAST    DC    CL2'/*'
ASTSL    DC    CL2'*/'
ONE      DC    F'1'
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
         DC    C' '
OUTAREA  DS    CL80
INRCDS   DC    PL7'0'
OUTRCDS  DC    PL7'0'
         LTORG
TIMED    DS    CL7
DATED    DS    CL9
DATETIME DS    4F
         ORG   DATETIME
TIME     DS    CL4
         DS    CL4
DATE     DS    CL5
         DS    CL3
HEADDATE DC    CL23' '
HEADDAY  DC    CL8' '
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GM,EODAD=EOFIN,DSORG=PS,LRECL=80
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,LRECL=80
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
         END   SMPUPD
