* ----------------------------------------------------------------- *
*       SMPFMUPD   -   ADAPTED FROM A COBOL PROGRAM BY SAM GOLOB    *
*                                                                   *
*                      THIS PROGRAM WILL FUNCTIONALLY REPLACE       *
*                      THE COBOL PROGRAM SMPFMUPD FROM FILE 118     *
*                      OF THE CBT MVS UTILITIES TAPE.               *
* ----------------------------------------------------------------- *
*                                                                   *
* INPUT - SYSUT1  - FBA-133 BLOCKED FILE WHICH IS THE REPORT FROM   *
*                   THE "PRINTER" DDNAME OF THE NEW PUTXREF PROGRAM *
*                                                                   *
*                                                                   *
* OUTPUT- SYSUT2  - FB-80 BLOCKED FILE CONTAINING ALL PTF NAMES     *
*                   GROUPED BY OWNING FMID.                         *
*                                                                   *
*       SYSPRINT -  NOT USED IN THIS RELEASE                        *
*                                                                   *
* ----------------------------------------------------------------- *
         GBLC  &LLEV
&LLEV    SETC  '001'
SMPFMUPD CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'SMPFMUPD - PUTXREF REPORT TO FORFMID LIST OF PTFS - '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
         DC    C'LEVEL &LLEV '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP THREE BASE REGISTERS
         USING SMPFMUPD,R12,R11,R10
         LR    R12,R15
         LR    R11,R12
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LA    R10,4095(,R11)
         LA    R10,1(,R10)
         LR    R1,R5          RESTORE PARM POINTER
         PRINT GEN
         OPEN  (SYSUT1,INPUT)
         OPEN  (SYSUT2,OUTPUT)
INITIT   DS    0H
         MVC   BLANKS,BLANKS-1
         MVC   INAREA,INAREA-1
         MVC   OUTAREA,OUTAREA-1
***********************************************************************
* GET SYSTEM DATE AND TIME                                            *
***********************************************************************
         TIME  DEC,DATETIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY
         OI    DATE+4,X'0F'
         UNPK  DATED,DATE
         OI    TIME+3,X'0F'
         UNPK  TIMED,TIME
*                          FILL HEADDATE AREA WITH INFO TO REUSE
         MVC   HEADDATE(2),DATED
         MVI   HEADDATE+2,C'/'
         MVC   HEADDATE+3(2),DATED+2
         MVI   HEADDATE+5,C'/'
         MVC   HEADDATE+6(4),DATED+4
         MVC   HEADDATE+12(2),TIMED
         MVI   HEADDATE+14,C':'
         MVC   HEADDATE+15(2),TIMED+2
         MVI   HEADDATE+17,C':'
         MVC   HEADDATE+18(2),TIMED+4
         MVC   HEADDAY(2),DATED
         MVI   HEADDAY+2,C'/'
         MVC   HEADDAY+3(2),DATED+2
         MVI   HEADDAY+5,C'/'
         MVC   HEADDAY+6(2),DATED+6
***********************************************************************
* -------
MAINLOOP DS    0H                         MAIN PROCESSING LOOP
INREPT   DS    0H
         GET   SYSUT1,INAREA
         CLC   INFMID(7),=C'FMID   '
         BE    INREPT
         CLC   INFMID(7),=C'*******'
         BE    INREPT
         CLC   INFMID(7),=C'       '
         BNE   INSAVEF
         CLC   IN1STPTF(7),=C'       '
         BE    INREPT
         B     INPROCC
INSAVEF  DS    0H
         CLC   IN1STPTF(7),=C'       '
         BNE   INREPT
         MVC   SND7STOR,SND7STOR-1
         MVC   PTFNAM,PTFNAM-1
         MVC   FMIDSTOR(7),INFMID
         MVC   UPDTELIN,UPDTELIN-1
         MVC   UPDTELT(14),UPDTELIT
         MVC   UPDTENAM(7),INFMID
         PUT   SYSUT2,UPDTELIN
         B     INREPT
INPROCC  DS    0H
         LA    R6,12
         LA    R7,IN1STPTF
INPRLOOP DS    0H
         CLC   0(7,R7),=C'       '
         BNE   INPRCOPY
         B     INREPT
INPRCOPY DS    0H
         MVC   PTFNAM(7),0(R7)
         BAL   R9,WRITEOUT
         LA    R7,10(,R7)
         BCT   R6,INPRLOOP
         B     INREPT
* -------
EOFIN    DS    0H
         CLOSE SYSUT1
         CLOSE SYSUT2
* -------
THEEND   DS    0H
         CLOSE SYSPRINT
         L     R13,4(,R13)
         RETURN (14,12),RC=0
         EJECT
* -------     SUBROUTINES
WRITEOUT DS    0H
         CLC   SND7STOR(7),PTFNAM         IS THIS THE SAME PTF AS LAST
         BE    WRITEOUE                   YES, DON'T WRITE ANYTHING
         MVC   OUTRECD,OUTRECD-1          CLEAR OUTPUT LINE
         MVC   OUTPTF(7),PTFNAM           MOVE IN PTF NAME
         MVC   OUTSLAST,SLAST             MOVE IN /*
         MVC   OUTFLITR,FLITR             MOVE IN LITERAL
         MVC   OUTFMID,FMIDSTOR           MOVE IN FMID
         MVC   OUTFLIT2,FLIT2             MOVE IN LITERAL
         MVC   OUTDATE(8),HEADDAY         MOVE IN DATE
         MVC   OUTASTSL,ASTSL             MOVE IN */
         PUT   SYSUT2,OUTRECD
WRITEOMV MVC   SND7STOR(7),PTFNAM         STORE THIS PTF NAME FOR NEXT
WRITEOUE BR    R9
* -------
*
*
* ----- WORK AREA ------ *
FLAG1    DC    X'00'
FLAG2    DC    X'00'
         DC    CL4' '
FMIDSTOR DC    CL7' '
         DC    CL4' '
SND7STOR DC    CL7' '
         DC    CL4' '
PTFNAM   DC    CL7' '
         DC    CL4' '
         DC    CL1' '
INAREA   DS    0CL133
INCNTL   DC    CL1' '
INFMID   DC    CL7' '
INFILL1  DC    CL3' '
IN1STPTF DC    CL7' '
INREST   DC    CL115' '
         DC    CL8' '
         DC    C' '
UPDTELIN DS    0CL80              IEBUPDTE ADD CARD
UPDTELT  DC    CL14' '
UPDTENAM DC    CL8' '
UPDTERST DC    CL58' '
         DC    CL8' '
OUTRECD  DS    0CL80
OUTRSTRT DC    CL6' '
OUTPTF   DC    CL7' '
OUTFILL1 DC    CL2' '
OUTSLAST DC    CL2' '
OUTFILL2 DC    CL2' '
OUTFLITR DC    CL7' '
OUTFMID  DC    CL7' '
OUTFLIT2 DC    CL24' '
OUTDATE  DC    CL10' '
OUTFILL3 DC    CL2' '
OUTASTSL DC    CL2' '
OUTFILL4 DC    CL9' '
         DC    CL8' '
* ----- CONSTANTS ------ *
UPDTELIT DC    C'./  ADD  NAME='
SLAST    DC    CL2'/*'
ASTSL    DC    CL2'*/'
FLITR    DC    CL7'FMID - '
FLIT2    DC    CL24' - FROM PUT TAPE - DONE '
ONE      DC    F'1'
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
         DC    C' '
OUTAREA  DS    CL133
INRCDS   DC    PL7'0'
OUTRCDS  DC    PL7'0'
         LTORG
TIMED    DS    CL7
DATED    DS    CL9
DATETIME DS    4F
         ORG   DATETIME
TIME     DS    CL4
         DS    CL4
DATE     DS    CL5
         DS    CL3
HEADDATE DC    CL23' '
HEADDAY  DC    CL8' '
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GM,EODAD=EOFIN,DSORG=PS,LRECL=133
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,LRECL=80
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
         END   SMPFMUPD
